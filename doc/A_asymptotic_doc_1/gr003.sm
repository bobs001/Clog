# macro read main.sm
# plot [or eps]
# 
# 
acoeff
    set x0=0.
    set x1=3.5

    set y0=0.
    set y1=0.15

    location 5000 29000 5000 22000
    expand 1.3 lweight 2 ltype 0 ctype black ptype 4 3
    limits $(x0) $(x1) $(y0) $(y1)
    box 1 2 0 0
    relocate $(x0) $(y1+0.006) ctype black label \
    "gr003.sm: T_e=100 keV  T_i=10  keV  n_e=10^{25} cm^{-3}"
# axes
    ctype black
    xlabel E_p (MeV)
    ylabel A   (MeV/\mu m)
#
#
    set Lmin=22
    set Lmax=4022
    data gr003.dat
    lines $(Lmin) $(Lmax)  read{Ep 2  Atot 3  Ae 4  Ai 5}
    set nn = 1000.
    set Ep = Ep/nn
    ltype 0 ctype red    connect Ep Atot
    ltype 0 ctype blue   connect Ep Ae
    ltype 0 ctype green  connect Ep Ai

# legend
#
    set dx=0.04
    set dy=-0.0004

    set x1=1.00 set y1=0.12
    set x2=1.50 set y2=0.12
    relocate $(x1) $(y1)  ltype 0 ctype red draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_{tot} "

    set y1=0.108
    set y2=0.108
    relocate $(x1) $(y1)  ltype 0 ctype blue draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_e"

    set y1=0.096
    set y2=0.096
    relocate $(x1) $(y1)  ltype 0 ctype green draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_I"



plot
   erase
   acoeff

eps
   erase
   device postencap gr003.eps
   acoeff
   hardcopy
   device x11
   erase
   plot
