# macro read main.sm
# plot [or eps]
# 
# 
acoeff


    set x0=0.
    set x1=5000.
    set y0=0.
    set y1=0.02


    location 5000 29000 5000 22000
    expand 1.3 lweight 2 ltype 0 ctype black ptype 4 3
    limits $(x0) $(x1) $(y0) $(y1)
    box 1 2 0 0
    relocate $(x0-0.00001) $(y1+0.001) ctype black label \
    "gr066.sm: T_e=100 keV  T_i=10  keV  n_e=10^{25} cm^{-3}"
# axes
    ctype black
    xlabel E_p (MeV)
    ylabel A_e   (MeV/\mu m)
#
#
    set Lmin=22
    set Lmax=5022
    data gr003.dat
    lines $(Lmin) $(Lmax)  read{Ep 2  Ae 4}
    set nn = 1000.
    set Ep = Ep/nn
    ltype 0 ctype blue   connect Ep Ae


    set Lmin=22
    set Lmax=5022
    data gr003.very.highE.dat
    lines $(Lmin) $(Lmax)  read{Exp 2  Aesmall 4}
    set nn = 1000.
    set Exp = Exp/nn
    ltype 0 ctype red   connect Exp Aesmall


# legend
#
    set dx=0
    set dy=0

    set x1= 500 set y1=0.01
    set x2=1500 set y2=0.01
    relocate $(x1) $(y1)  ltype 0 ctype blue draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_e "
    set y1=0.0065
    set y2=0.0065
    relocate $(x1) $(y1)  ltype 0 ctype red draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_e^{asymptotic} "


plot
   erase
   acoeff

eps
   erase
   device postencap gr066.eps
   acoeff
   hardcopy
   device x11
   erase
   plot
