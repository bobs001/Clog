# macro read main.sm
# plot [or eps]
# 
# 
acoeff

    set x0=0.     # MeV
    set x1=50.    # MeV
    set y1= 0.010 # MeV/micron
    set y0=-0.060 # MeV/micron

#    set gx1=5000 
#    set gx2=30000 
#    set gy1=5000 
#    set gy2=25000 
#    location $gx1 $gx2 $gy1 $gy2 
    location 5000 29000 5000 22000

    expand 1.3 lweight 2 ltype 0 ctype black ptype 4 3
    limits $(x0) $(x1) $(y0) $(y1)
    box 1 2 0 0
    relocate $(x0-0.001) $(y1+0.002) ctype black label \
    "gr041.sm: T_e=10 keV  T_i=10  keV  n_e=10^{25} cm^{-3}"
# axes
    ctype black
    xlabel E_p (MeV)
    ylabel A_{q,e}   (MeV/\mu m)
#
#
    set Lmin=22
    set Lmax=3022
    data gr001.dat
    lines $(Lmin) $(Lmax)  read{Ep 2  Aqe 10 Aqi 11}
    set nn = 1000.
    set Ep = Ep/nn
    ltype 0 ctype blue   connect Ep Aqe


    set Lmin=22
    set Lmax=3022
    data gr001.smallE.dat
    lines $(Lmin) $(Lmax)  read{Ep 2  Aqesmall 10  Aqismall 11}
    set nn = 1000.
    set Ep = Ep/nn
    ltype 0 ctype red   connect Ep Aqesmall

#
# y=0 axis
#
    relocate $(x0) 0.0  ltype 0 ctype black draw $(x1) 0.0

# legend
#
    set dx=0.002
    set dy=-0.1E-5

    set x1=20. 
    set x2=30. 
    set y1=-0.008
    set y2=-0.008
    relocate $(x1) $(y1)  ltype 0 ctype blue draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_{q,e} "
    set y1=-0.016
    set y2=-0.016
    relocate $(x1) $(y1)  ltype 0 ctype red draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_{q,e}^{asymptotic} "

plot
   erase
   acoeff

eps
   erase
   device postencap gr041.eps
   acoeff
   hardcopy
   device x11
   erase
   plot
