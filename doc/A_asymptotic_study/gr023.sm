# macro read main.sm
# plot [or eps]
# 
# 
acoeff

    set x0=0.
    set x1=0.015
    set y0=0.
    set y1=0.0001


    location 5000 29000 5000 22000
    expand 1.3 lweight 2 ltype 0 ctype black ptype 4 3
    limits $(x0) $(x1) $(y0) $(y1)
    box 1 2 0 0
    relocate $(x0-0.00001) $(y1+1.E-5) ctype black label \
    "gr023.sm: T_e=100 keV  T_i=10  keV  n_e=10^{25} cm^{-3}"
# axes
    ctype black
    xlabel E_p (MeV)
    ylabel A_{c,e}   (MeV/\mu m)
#
#
    set Lmin=22
    set Lmax=5022
    data gr003.dat
    lines $(Lmin) $(Lmax)  read{Ep 2  Ace 7}
    set nn = 1000.
    set Ep = Ep/nn
    ltype 0 ctype blue   connect Ep Ace


    set Lmin=22
    set Lmax=5022
    data gr003.smallE.dat
    lines $(Lmin) $(Lmax)  read{Exp 2  Acesmall 7}
    set nn = 1000.
    set Exp = Exp/nn
    ltype 0 ctype red   connect Exp Acesmall


# legend
#
    set dx=0
    set dy=0

    set x1=0.0025 set y1=4.E-5
    set x2=0.0070 set y2=4.E-5
    relocate $(x1) $(y1)  ltype 0 ctype blue draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_{c,e} "

    set y1=2.5E-5
    set y2=2.5E-5
    relocate $(x1) $(y1)  ltype 0 ctype red draw $(x2) $(y2)
    relocate $(x2+dx) $(y2+dy)  ctype black label "A_{c,e}^{asymptotic} "


plot
   erase
   acoeff

eps
   erase
   device postencap gr023.eps
   acoeff
   hardcopy
   device x11
   erase
   plot
